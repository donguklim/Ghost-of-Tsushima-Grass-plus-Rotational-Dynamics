
#define PI_VALUE 3.14159265358979323846
# define ROD_DOT_GROUND_THRESHOLD 0.3


struct GrassMotionShaderFunctions
{
    
    float4 QuaternionFromAxisAngle(float3 axis, float angle)
    {
        float halfAngle = angle * 0.5;
        float s = sin(halfAngle);
        return float4(axis * s, cos(halfAngle));
    }
    
    float4 QuaternionFromRotationalVector(float3 vec)
    {
        float angle = length(vec);
        vec = angle > 0 ? vec / angle : vec;
        return QuaternionFromAxisAngle(vec, angle);

    }

    float4 QuaternionMultiply(float4 q1, float4 q2)
    {
        return float4(
            q1.w * q2.xyz + q2.w * q1.xyz + cross(q1.xyz, q2.xyz),
            q1.w * q2.w - dot(q1.xyz, q2.xyz)
        );
    }

    float3 RotateVectorByQuaternion(float3 v, float4 q)
    {
        float3 qVec = q.xyz;
        float3 uv = cross(qVec, v);
        float3 uuv = cross(qVec, uv);
        return v + ((uv * q.w) + uuv) * 2.0;
    }
    
    float3 GetCollidingAngleDelta(float3 rod_dir, float3 angular_disp_delta)
    {
        float angle = length(angular_disp_delta);
        if (angle < 0.001)
            return 0;
        
        float3 rot_axis = angular_disp_delta / angle;
        
        //float3 ground_norm = float3(0, 1, 0);
        
        
        float3 rot_axis_c_rod = cross(rot_axis, rod_dir);
        
        float a_val = rot_axis_c_rod.z; // dot product with ground norm
        
        float b_val = -cross(rot_axis, rot_axis_c_rod).z; // dot product with ground norm
        
        // dot(ground_norm, rod_direction after angular_velocity is applied)
        // = a_val * sin(angle * delta_time) + b_val * cos(angle * delta_time) + b_val + dot(current rod_direction, ground_norm)
        
        // we don't want anbove value to have value less than ROD_GROUND_DOT_THRESHOLD
        
        // we  want
        // dot(ground_norm, rod_direction after angular_velocity is applied) > ROD_DOT_GROUND_THRESHOLD
        
        float c_val = ROD_DOT_GROUND_THRESHOLD - rod_dir.z - b_val;
        
        // We want to solve x for  a * sin(x) + b * cos(x) = c
                
        float abs_a_val = abs(a_val);
        float abs_b_val = abs(b_val);
        
        // changing magnitude of angular velocity won't significantly lift the rod from the ground
        if (abs_a_val < 0.001 && abs_b_val < 0.001)
        {
            return angular_disp_delta;
        }
        // Solve b * cos(x) = c
        else if (abs_a_val < 0.001)
        {
            return rot_axis * acos(c_val / b_val);
        }
        // Solve a * sin(x) = c
        else if (abs_b_val < 0.001)
        {
            return rot_axis * asin(c_val / a_val);
        }
        
        // Given a * sin(x) + b * cos(x) = c
        // using trigonometric identity sin(i + j) = sin(i)cos(j) = cos(i)sin(j)
        // R * sin(x + z) = c,  where tan(z) = b/a and R = sqrt(a^2 + b^2)
        // x = asin(c/R) + z
        
        return rot_axis * asin(c_val / sqrt(a_val * a_val + b_val * b_val)) - atan(b_val / a_val);

    }
    
    float ShiftToPIAtMaxAngle(float angle)
    {
        angle = fmod(angle, 2.0 * PI_VALUE);

        if (angle > PI_VALUE)
        {
            angle -= 2.0 * PI_VALUE;
        }
        else if (angle < -PI_VALUE)
        {
            angle += 2.0 * PI_VALUE;
        }

        return angle;
    }

    float ShiftToPositiveAngle(float angle)
    {
        angle = fmod(angle, 2.0 * PI_VALUE);

        if (angle < 0)
        {
            angle += 2.0 * PI_VALUE;
        }

        return angle;
    }

    float3 RotateY(float3 vec, float angle)
    {
        float s = sin(angle);
        float c = cos(angle);
    
        float3x3 rotationMatrix = float3x3(
            c, 0, s,
            0, 1, 0,
            -s, 0, c
        );
    
        return mul(rotationMatrix, vec);
    }
    
    float3 RotateZ(float3 vec, float angle)
    {
        float s = sin(angle);
        float c = cos(angle);
    
        float3x3 rotationMatrix = float3x3(
             c, -s, 0,
             s, c, 0,
             0, 0, 1
        );
    
        return mul(rotationMatrix, vec);
    }
    
    void GetBezierPoints(float2 p1_xz, float2 p2_xz, float4 angular_disp, out float3 p1, out float3 p2, out float3 side_dir)
    {
        p1 = float3(p1_xz.x, 0, p1_xz.y);
        p2 = float3(p2_xz.x, 0, p2_xz.y);
        
        float4 p1_quat = QuaternionFromRotationalVector(angular_disp.xyz);
        float4 p1p2_quat = QuaternionFromAxisAngle(float3(0, 1, 0), angular_disp.w);
        
        float3 p1p2 = RotateVectorByQuaternion(p2 - p1, p1p2_quat);
        p2 = p1 + p1p2;
        
        p1 = RotateVectorByQuaternion(p1, p1_quat);
        p2 = RotateVectorByQuaternion(p2, p1_quat);
        
        side_dir = RotateVectorByQuaternion(float3(0, 1, 0), p1_quat);
        
    }
    
    float3 GetDistantRodTorque(
        float damping_coefficient,
        float3 wind_force,
        float r2_stiffness,
        float r2_angular_velocity,
        float r2_angular_displacement,
        float3 r2_rotational_axis,
        float r1_stiffness,
        float3 r1_angular_velocity,
        float3 r1_angular_displacement,
        float3 r1_end,
        float3 r2_end
    )
    {
        float3 r2 = (r2_end - r1_end);
        float r2_length = length(r2);
        float r2_length_square = r2_length * r2_length;
        float3 r2_dir = r2;
        
        float3 r2_torque_dir = cross(r2_rotational_axis, r2_dir);
        
        float3 l0_force_term = wind_force - 2.0 * r2_stiffness * r2_angular_displacement / r2_length_square * r2_torque_dir - damping_coefficient * cross(r1_angular_velocity, r1_end);
        float3 l1_force_term = -damping_coefficient * r2_angular_velocity * r2_torque_dir - damping_coefficient * cross(r1_angular_velocity, r2_dir);
        float3 l0_term = cross(r1_end, l0_force_term);
        float3 l1_term = cross(r2_dir, l0_force_term) + cross(r1_end, l1_force_term);
        float3 l2_term = cross(r2_dir, l1_force_term);
        
        
        return l0_term * r2_length + l1_term * r2_length_square / 2.0 + l2_term * r2_length * r2_length_square / 3.0;

    }
    float3 GetPivotRodTorque(
        float damping_coff,
        float stiffness,
        float3 wind_force,
        float3 rod,
        float3 angular_velocity,
        float3 angular_disp
    )
    {
        float rod_length = length(rod);
        float3 wind_torque = cross(rod, wind_force) * rod_length / 2.0;
        float3 damping_torque = -damping_coff * cross(rod, cross(angular_velocity, rod)) * rod_length / 3.0;
       
        return rod_length * (cross(rod, wind_force) / 2.0 - damping_coff * cross(rod, cross(angular_velocity, rod)) / 3.0) - stiffness * angular_disp;
       
    }
    
    
    float GetMomentOfInertia(float mass, float3 r1_end, float3 r2_end)
    {
        float3 r2 = r2_end - r1_end;
        float r2_length = length(r2);
        float r1_length = length(r1_end);
        float r2_center_distance = length(r2 / 2 + r1_end);
        
        float r2_mass = mass * r2_length / (r1_length + r2_length);
        
        // Moment of Inertia caused by R2 rod
        // = Moment of Inertia of R2 at its center of mass + (R2 center of mass distance from the pivot)^2
        //
        // Moment of Inertia of R2 at its center of mass = r2_mass * r2_length * r2_length / 12.0
        float r2_MI = r2_mass * (r2_length * r2_length / 12.0 + r2_center_distance * r2_center_distance);
        
        float r1_MI = (mass - r2_mass) / 3.0 * r1_length * r1_length;
        
        return r1_MI + r2_MI;
    
    }
 
    void GrassUpdate(
        float damping_coff,
        float stiffness,
        float p2_stiffness,
        float mass,
        float time_delta,
        float3 wind,
        float2 p1_xz,
        float2 p2_xz,
        inout float4 angular_velocity,
        inout float4 angular_disp,
        out float3 wind_z_force
    )
    {
        float swing_threshold = PI_VALUE * 0.98;
        float p1_threshold_low = PI_VALUE * 0.02;
        float p1_threshold_high = PI_VALUE * 1.98;
        float p1p2_threshold = PI_VALUE * 0.02;
        
        if (p1_xz.x < 0)
        {
            p1_xz.x = 0.01;
        }
        float p1_static_bending = atan(p1_xz.y / p1_xz.x);
        
        float2 p1p2_xz = p2_xz - p1_xz;
        float p1p2_static_bending = acos(dot(-p1_xz, p1p2_xz) / (length(p1_xz) * length(p1p2_xz)));

        float3 p1 = float3(0, 0, 0);
        float3 p2 = float3(0, 0, 0);
        float3 r2_rotational_axis = float3(0, 0, 0);
        GetBezierPoints(p1_xz, p2_xz, angular_disp, p1, p2, r2_rotational_axis);
        float3 p1p2 = p2 - p1;
        
        float3 p1_dir = p1 / length(p1);
        
        wind_z_force = r2_rotational_axis * 20;
        
        float3 p1_acc = GetPivotRodTorque(
            damping_coff,
            stiffness,
            wind, 
            p1,
            angular_velocity.xyz,
            angular_disp.xyz
        ) + GetDistantRodTorque(
            damping_coff,
            wind,
            p2_stiffness,
            angular_velocity.w,
            angular_disp.w,
            r2_rotational_axis,
            stiffness,
            angular_velocity.xyz,
            angular_disp.xyz,
            p1,
            p2
        ) / GetMomentOfInertia(mass, p1, p2);
        
        /*
        float3 p1_acc = GetPivotRodTorque(
            damping_coff,
            stiffness,
            wind_coff * wind,
            p1,
            angular_velocity.xyz,
            angular_disp.xyz
        ) / GetMomentOfInertia(mass, p1, p2);
        */
        float p1_length = length(p1);
        float p1p2_length = length(p1p2);
        
        float legnth_sum = p1_length + p1p2_length;
        float p1_mass = mass * p1_length / legnth_sum;
        float p1p2_mass = mass * p1p2_length / legnth_sum;
        
        
        float3 p1p2_bending_acc = GetPivotRodTorque(
            damping_coff,
            p2_stiffness,
            wind,
            p1p2,
            angular_velocity.w * r2_rotational_axis,
            angular_disp.w * r2_rotational_axis
        ) / (p1p2_mass * p1p2_length * p1p2_length / 3.0);
        
        
        float4 velocity_delta = float4(p1_acc, dot(r2_rotational_axis, p1p2_bending_acc)) * time_delta;
        angular_velocity += velocity_delta;
        
        float4 angle_disp_delta = angular_velocity * time_delta;
        
        float3 new_p1_dir = RotateVectorByQuaternion(p1_dir, QuaternionFromRotationalVector(angle_disp_delta.xyz));
        
        
        if (new_p1_dir.z < ROD_DOT_GROUND_THRESHOLD && abs(p1_dir.z - ROD_DOT_GROUND_THRESHOLD) < 0.1)
        {
            angular_velocity.xy = float2(0, 0);
            angle_disp_delta.xy = float2(0, 0);

        }
        else if (new_p1_dir.z < ROD_DOT_GROUND_THRESHOLD)
        {
            angle_disp_delta.xyz = GetCollidingAngleDelta(p1_dir, angle_disp_delta.xyz);
            angular_velocity.xy = float2(0, 0);
        }

        angular_disp += angle_disp_delta;
        
    }
};
  